import java.text.SimpleDateFormat

apply plugin: 'java'

group = 'net.thirdshift'
 
println """
==========================================================================
You are building the Tokens plugin https://github.com/TheCoolestPaul/Tokens
  Version:    ${project.version} 
  Build time: ${getCurrentTimestamp()}

Output files are located in [project]/build/libs/Tokens-${project.version}.jar
==========================================================================
"""

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    flatDir {
        dirs "libs"
    }
    maven {
        name = 'placeholderapi-repo'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = "Vault-API"
        url "http://nexus.hc.to/content/repositories/pub_releases"
    }
    maven {
        name = "Jitpack-io"
        url = "https://jitpack.io"
    }
    maven {
        name = "codeMC repo"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        name = "maven NMS"
        url = "https://repo.codemc.io/repository/nms/"
    }
    maven {
        name = "factions repo"
        url = "https://repo.dustplanet.de/artifactory/ext-release-local/"
    }

    // Added for Vault dependencies
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    implementation "org.spigotmc:spigot:$spigotVersion"
    implementation ("net.milkbowl.vault:VaultAPI:$vaultVersion") {
        exclude group: "org.bukkit"
    }
    implementation ("me.clip:placeholderapi:$placeholderVersion") {
        exclude group: "org.bstats"
    }
    implementation ("com.github.mcMMO-Dev:mcMMO:$mcmmoVersion") {
        exclude group: "com.sk89q.worldguard"
    }
    implementation "com.SirBlobman.combatlogx:CombatLogX-API:$combatlogxVersion"
    implementation "com.massivecraft:Factions:$factionsVersion"
    implementation "com.github.brcdev-minecraft:shopgui-api:$shopguiplusVersion"
    implementation "sh.okx.rankup:Rankup:3.12.2"

    testCompile "junit:junit:4.12"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version
        include 'plugin.yml'
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

static def getCurrentTimestamp() {
  SimpleDateFormat df = new SimpleDateFormat ("HH:mm:ss MM-dd-yyyy")
  return df.format (new Date())
}